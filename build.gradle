// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext {
        agpVersion = '7.3.1'
        androidXTestVersion = '1.4.0'

        moshi = '1.14.0'
        moshix = '1.7.20-Beta-0.18.3'
        room = '2.4.3'
        hilt_version = '2.44'
        retrofit = "2.9.0"
        okhttp = '5.0.0-alpha.10'
        epoxy = '5.0.0'
        flipper = '0.170.0'
        coroutines = '1.6.4'
        mockk = '1.13.2'
        core_ktx = '1.9.0'
        appcompat = '1.5.1'
        activity = '1.6.0'
        material = "1.7.0-beta01"
        junit = "4.13.2"
        truth = "1.1.3"
        turbine = '0.12.0'
        viewmodel_ktx = '2.6.0-alpha01'
        compose = "1.3.0"
        compose_m3 = '1.1.0-alpha01'
        compose_material = '1.3.0'
        compose_compiler = '1.3.0'
        immutable_collection = '0.3.5'
    }
}

plugins {
    id 'com.android.application' version "$agpVersion" apply false
    id 'com.android.library' version "$agpVersion" apply false
    id 'org.jetbrains.kotlin.android' version '1.7.10' apply false
    id 'com.google.devtools.ksp' version "1.7.10-1.0.6" apply false
    id 'com.android.test' version "$agpVersion" apply false
    id 'com.google.dagger.hilt.android' version "$hilt_version" apply false
}

subprojects {
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile)
            .matching {
                !it.name.startsWith("kapt")
            }
            .configureEach {
                kotlinOptions {
                    if (project.findProperty("hackernews.enableComposeCompilerReports") == "true") {
                        freeCompilerArgs += [
                                "-P",
                                "plugin:androidx.compose.compiler.plugins.kotlin:metricsDestination=" +
                                        project.buildDir.absolutePath + "/compose_metrics"
                        ]
                        freeCompilerArgs += [
                                "-P",
                                "plugin:androidx.compose.compiler.plugins.kotlin:reportsDestination=" +
                                        project.buildDir.absolutePath + "/compose_metrics"
                        ]
                    }
                }
            }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}